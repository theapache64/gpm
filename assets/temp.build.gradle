plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlin_version"
}

allOpen {
    annotation("com.thinkpalm.test.movieapp.utils.test.OpenForTesting")
}

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "com.thinkpalm.test.movieapp"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 10001
        versionName "1.0.0-alpha01"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // To make `sharedTest` accessible from both unit test and instrumentation test
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues true
        }
    }
}

dependencies {

    //Core
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation "com.google.android.material:material:$material_version"

    // Coroutines
    def coroutines_version = '1.3.5'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Lifecycle extension
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Dagger 2
    def dagger_version = '2.27'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"


    // MaterialColors
    implementation 'com.theah64.materialcolors:materialcolors:1.0.0'

    // TwinKill
    def twinkill_version = '1.1.0-alpha05'
    implementation "com.theapache64.twinkill:core:$twinkill_version"
    testImplementation "com.theapache64.twinkill:test:$twinkill_version"
    androidTestImplementation "com.theapache64.twinkill:test:$twinkill_version"
    implementation "com.theapache64.twinkill:logger:$twinkill_version"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"

    // Test
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    testImplementation('com.winterbe:expekt:0.5.0') {
        exclude group: "org.jetbrains.kotlin"
    }

    // Robolectric
    testImplementation "org.robolectric:robolectric:4.3.1"

    // Mocktio Kotlin
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    // Arch testing
    def core_testing_version = '2.1.0'
    testImplementation "androidx.arch.core:core-testing:$core_testing_version"
    androidTestImplementation "androidx.arch.core:core-testing:$core_testing_version"
    testImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation('com.schibsted.spain:barista:3.4.0') {
        exclude group: 'org.jetbrains.kotlin'
    }

    // DaggerMock
    testImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.5'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.5'
    testImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.5'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.5'
}
